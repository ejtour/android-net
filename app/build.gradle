apply plugin: 'com.android.application'

def cmd = 'git rev-list HEAD --count'
def gitVersion = cmd.execute().text.trim().toInteger()

android {
    compileSdkVersion COMPILESDKVERSION as int
    defaultConfig {
        applicationId "com.hll_sc_app"
        minSdkVersion MINSDKVERSION as int
        targetSdkVersion TARGETSDKVERSION as int
        versionCode gitVersion
        versionName "3.22.0"
        multiDexEnabled true
        flavorDimensions "default"
        packagingOptions {
            exclude 'META-INF/services/javax.annotation.processing.Processor'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/NOTICE'
        }
        javaCompileOptions {
            annotationProcessorOptions {
                annotationProcessorOptions { includeCompileClasspath = true }
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        ndk {
            abiFilters 'armeabi-v7a'
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('22citysupply.keystore')
            storePassword STOREPASSWORD
            keyAlias KEYALIAS
            keyPassword KEYPASSWORD
        }
    }

    buildTypes {
        release {
            buildConfigField "boolean", "isDebug", "false"
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".beta"
            buildConfigField "boolean", "isDebug", "true"
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        official {
            buildConfigField "String", "ODM_NAME", '"二十二城"'
            buildConfigField "boolean", "isOdm", "false"
        }

        odm0 { // odm 演示版本，各种 key 适用于正式版
            applicationId "com.odm_sc_app.beta"
            buildConfigField "String", "ODM_NAME", '"ODM公司专用账号"'
            buildConfigField "boolean", "isOdm", "true"
        }

        odm1 { // odm 测试版本 各种 key 适用于测试版
            applicationId "com.odm_sc_app"
            buildConfigField "String", "ODM_NAME", '"ODM45"'
            buildConfigField "boolean", "isOdm", "true"
        }

        odm2 {
            applicationId "com.odm_sc_app_2"
            buildConfigField "String", "ODM_NAME", '"立蚂送"'
            buildConfigField "boolean", "isOdm", "true"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    configurations {
        all {
            exclude group: 'xpp3', module: 'xpp3'
        }
    }

    configurations.all {
        resolutionStrategy {
            force 'com.android.support:support-v4:27.1.0',
                    'com.android.support:appcompat-v7:27.1.0',
                    'com.android.support:cardview-v7:27.1.0',
                    'com.android.support:support-annotations:27.1.0',
                    'com.android.support:design:27.1.0'
        }
    }
}

private static String getApkName(variant) {
    def prefix
    switch (variant.flavorName) {
        case "odm2":
            prefix = "lms"
            break
        default:
            prefix = "22city"
            break
    }
    "${prefix}_sc_v${variant.versionName}"
}

gradle.buildFinished {
    def debugFlavors = ["odm0", "odm1"]
    android.applicationVariants.all { variant ->
        def outputApkName = getApkName(variant)
        if (variant.buildType.name.equalsIgnoreCase("release") && !debugFlavors.contains(variant.flavorName)) {
            copy {
                def destPath = "../../supplier-apk/${variant.versionName}"
                from variant.outputs.first().outputFile
                into destPath
                rename { String fileName ->
                    fileName.endsWith(".apk") ? "${outputApkName}.apk" : fileName
                }

                from "${buildDir}/outputs/mapping/${variant.dirName}/mapping.txt"
                into destPath
                rename { String fileName ->
                    fileName.replace("mapping.txt", "${outputApkName}_mapping.txt")
                }

                from "${buildDir}/intermediates/symbols/${variant.dirName}/R.txt"
                into destPath
                rename { String fileName ->
                    fileName.replace("R.txt", "${outputApkName}_R.txt")
                }
            }
        }
    }
}

dependencies {
    implementation project(':base')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.zxing:core:3.3.2'
    testImplementation 'junit:junit:4.12'
    // noinspection GradleCompatible
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // ButterKnife
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    implementation 'com.jakewharton:butterknife:8.4.0'
    // arouter
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'
    //chart插件
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    // switchbutton
    implementation 'com.kyleduo.switchbutton:library:2.0.0'

    //微信sdk
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:+'
    //qq sdk
    implementation 'com.tencent.tauth:qqopensdk:3.51.2'

    //圆角textview
    implementation 'com.github.QQzs:BorderView:1.0.6'

    // IM
    implementation 'org.igniterealtime.smack:smack-tcp:4.3.3'
    implementation 'org.igniterealtime.smack:smack-android:4.3.3'
    implementation 'org.igniterealtime.smack:smack-experimental:4.3.3'
    implementation 'cn.jiguang.imui:messagelist:0.8.0'
    implementation 'cn.jiguang.imui:chatinput:0.10.0'

    // bugly-upgrade
    implementation 'com.tencent.bugly:crashreport_upgrade:1.5.0'

    implementation 'top.zibin:Luban:1.1.8'

    implementation 'com.aliyun.dpa:oss-android-sdk:+'
    // 角标
    implementation 'me.leolin:ShortcutBadger:1.1.22@aar'

    // 地图
    implementation 'com.amap.api:3dmap:latest.integration'
}
